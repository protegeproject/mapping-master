
options {
  LOOKAHEAD = 1;
  IGNORE_CASE = true;
  STATIC = false;
  USER_TOKEN_MANAGER=false;
  USER_CHAR_STREAM=false;

  // JJTree options

  MULTI=true;
  NODE_DEFAULT_VOID = true;
}

PARSER_BEGIN(MappingMasterParser)

package org.mm.parser;

import org.mm.core.settings.ReferenceSettings;

import java.util.*;

public class MappingMasterParser 
{
  private String defaultLocationValue = "";
  private String defaultLiteral = "";
  private String defaultID = "";
  private String defaultLabel = "";
  private String defaultLanguage = "";
  private String defaultPrefix = "";
  private String defaultNamespace = "";

  private int defaultReferenceType = -1;
  private int defaultPropertyType = -1;
  private int defaultPropertyValueType = XSD_STRING;
  private int defaultDataPropertyValueType = XSD_STRING;
  private int defaultAnnotationPropertyValueType = XSD_STRING;

  private int defaultValueEncoding = -1;
  private int defaultIRIEncoding = -1;
  private int defaultClassValueEncoding = -1;
  private int defaultPropertyValueEncoding = -1;
  private int defaultObjectPropertyValueEncoding = -1;
  private int defaultDataPropertyValueEncoding = -1;
  private int defaultIndividualValueEncoding = -1;
  private int defaultShiftSetting = -1;
  private int defaultEmptyLocationSetting = -1;
  private int defaultEmptyLiteralSetting = -1;
  private int defaultEmptyRDFIDSetting = -1;
  private int defaultEmptyRDFSLabelSetting = -1;
  private int defaultIfOWLEntityExistsSetting = -1;
  private int defaultIfOWLEntityDoesNotExistSetting = -1;
  
  private ReferenceSettings settings;
  
  public MappingMasterParser(java.io.InputStream stream, ReferenceSettings settings, int unused)
  {
    this(stream);
    changeGlobalReferenceSettings(settings);
  }

  public void changeGlobalReferenceSettings(ReferenceSettings settings)
  {
    defaultLanguage = settings.getLanguage();
    defaultPrefix = settings.getPrefix();
    defaultNamespace = settings.getNamespace();
    defaultReferenceType = settings.getReferenceTypeSetting().getConstant();
    defaultPropertyType = settings.getPropertyTypeSetting().getConstant();
    defaultValueEncoding = settings.getValueEncodingSetting().getConstant();
    defaultIRIEncoding = settings.getIRIEncodingSetting().getConstant();
    defaultClassValueEncoding = settings.getValueEncodingSetting().getConstant();
    defaultPropertyValueEncoding = settings.getValueEncodingSetting().getConstant();
    defaultObjectPropertyValueEncoding = settings.getValueEncodingSetting().getConstant();
    defaultDataPropertyValueEncoding = settings.getValueEncodingSetting().getConstant();
    defaultIndividualValueEncoding = settings.getValueEncodingSetting().getConstant();
    defaultShiftSetting = settings.getShiftSetting().getConstant();
    defaultEmptyLocationSetting = settings.getEmptyLocationSetting().getConstant();
    defaultEmptyLiteralSetting = settings.getEmptyLiteralSetting().getConstant();
    defaultEmptyRDFIDSetting = settings.getEmptyRDFIDSetting().getConstant();
    defaultEmptyRDFSLabelSetting = settings.getEmptyRDFSLabelSetting().getConstant();
    defaultIfOWLEntityExistsSetting = settings.getIfOWLEntityExistsSetting().getConstant();
    defaultIfOWLEntityDoesNotExistSetting = settings.getIfOWLEntityDoesNotExistSetting().getConstant();
  }
}

PARSER_END(MappingMasterParser)

/* White space */

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

/* Comments */

MORE :
{
  "//" : IN_SINGLE_LINE_COMMENT
|  <"/**" ~["/"]> { input_stream.backup(1); } : IN_FORMAL_COMMENT
|  "/*" : IN_MULTI_LINE_COMMENT
}

<IN_SINGLE_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: "\n" | "\r" | "\r\n" > : DEFAULT
}

<IN_FORMAL_COMMENT>
SPECIAL_TOKEN :
{
  <FORMAL_COMMENT: "*/" > : DEFAULT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}

<IN_SINGLE_LINE_COMMENT,IN_FORMAL_COMMENT,IN_MULTI_LINE_COMMENT>
MORE :
{
  < ~[] >
}

/* Tokens */
TOKEN: { <CLASS: "Class:" | "class:"> }
TOKEN: { <INDIVIDUAL: "Individual:" | "individual:"> }
TOKEN: { <EQUIVALENT_TO: "EquivalentTo:" | "equivalentTo:"> }
TOKEN: { <SUBCLASS_OF: "SubClassOf:" | "subClassOf:"> }
TOKEN: { <FACTS: "Facts:" | "facts:"> }
TOKEN: { <TYPES: "Types:" | "types:"> }
TOKEN: { <ANNOTATIONS: "Annotations:" | "annotations:"> }
TOKEN: { <SAME_AS: "SameAs:" | "sameAs:"> }
TOKEN: { <DIFFERENT_FROM: "DifferentFrom:" | "differentFrom:"> }

/* Class expression tokens */

TOKEN: {<SOME: "some" | "SOME">}
TOKEN: {<ONLY: "only" | "ONLY">}
TOKEN: {<VALUE: "value" | "VALUE">}
TOKEN: {<MIN: "min" | "MIN">}
TOKEN: {<MAX: "max" | "MAX">}
TOKEN: {<EXACTLY: "exactly" | "EXACTLY">}
TOKEN: {<AND: "and" | "AND">}
TOKEN: {<OR: "or" | "OR">}
TOKEN: {<NOT: "not" | "NOT">}

TOKEN: {<TRUE: "true" | "TRUE">}
TOKEN: {<FALSE: "false" | "FALSE">}

TOKEN: { <XSD_STRING: "xsd:string" > }
TOKEN: { <XSD_DECIMAL: "xsd:decimal" > }
TOKEN: { <XSD_BYTE: "xsd:byte" > }
TOKEN: { <XSD_SHORT: "xsd:short" > }
TOKEN: { <XSD_INTEGER: "xsd:integer" > }
TOKEN: { <XSD_LONG: "xsd:long" > }
TOKEN: { <XSD_FLOAT: "xsd:float"  > }
TOKEN: { <XSD_DOUBLE: "xsd:double" > }
TOKEN: { <XSD_BOOLEAN: "xsd:boolean" > }
TOKEN: { <XSD_DATETIME: "xsd:dateTime"> }
TOKEN: { <XSD_TIME: "xsd:time"> }
TOKEN: { <XSD_DURATION: "xsd:duration" > }
TOKEN: { <XSD_DATE: "xsd:date" > }
TOKEN: { <RDF_XMLLITERAL: "rdf:XMLLiteral" > }
TOKEN: { <RDF_PLAINLITERAL: "rdf:PlainLiteral" > }

/*
TOKEN: { <XSD_NOTATION: "xsd:NOTATION" > }
TOKEN: { <XSD_UNSIGNEDLONG: "xsd:unsignedLong" > }
TOKEN: { <XSD_ANYSIMPLETYPE: "xsd:anySimpleType" > }
TOKEN: { <XSD_HEXBINARY: "xsd:hexBinary" > }
TOKEN: { <XSD_NONPOSITIVEINTEGER: "xsd:nonPositiveInteger" > }
TOKEN: { <XSD_NONNEGATIVEINTEGER: "xsd:nonNegativeInteger" > }
TOKEN: { <XSD_NORMALIZEDSTRING: "xsd:normalizedString" > }
TOKEN: { <XSD_POSITIVEINTEGER: "xsd:positiveInteger" > }
TOKEN: { <XSD_ENTITY: "xsd:ENTITY" > }
TOKEN: { <XSD_LANGUAGE: "xsd:language" > }
TOKEN: { <XSD_GMONTHDAY: "xsd:gMonthDay" > }
TOKEN: { <XSD_ANYURI: "xsd:anyURI" > }
TOKEN: { <XSD_NAME: "xsd:Name" > }
TOKEN: { <XSD_GYEARMONTH: "xsd:gYearMonth" > }
TOKEN: { <XSD_QNAME: "xsd:QName" > }
TOKEN: { <XSD_NEGATIVEINTEGER: "xsd:negativeInteger" > }
TOKEN: { <XSD_BASE64BINARY: "xsd:base64Binary" > }
TOKEN: { <XSD_ID: "xsd:ID" > }
TOKEN: { <XSD_TOKEN: "xsd:token" > }
TOKEN: { <XSD_UNSIGNEDINT: "xsd:unsignedInt" > }
TOKEN: { <XSD_IDREF: "xsd:IDREF" > }
TOKEN: { <XSD_UNSIGNEDBYTE: "xsd:unsignedByte" > }
TOKEN: { <XSD_GDAY: "xsd:gDay" > }
TOKEN: { <XSD_GYEAR: "xsd:gYear" > }
TOKEN: { <XSD_NCNAME: "xsd:NCName" > }
TOKEN: { <XSD_BYTE: "xsd:byte" > }
TOKEN: { <XSD_GMONTH: "xsd:gMonth" > }
TOKEN: { <XSD_UNSIGNEDSHORT: "xsd:unsignedShort" > }
TOKEN: { <XSD_NMTOKEN: "xsd:NMTOKEN" > }
*/

/* MappingMaster tokens */

TOKEN: { <MM_LOCATION_WITH_DUPLICATES: "mm:LocationWithDuplicates"> }
TOKEN: { <MM_LOCATION: "mm:Location"> }

TOKEN: { <RDF_ID: "rdf:ID"> }
TOKEN: { <RDFS_LABEL: "rdfs:label"> }

TOKEN: { <XML_LANG: "xml:lang" > }
TOKEN: { <MM_NULL: "mm:null" > }

TOKEN: { <MM_LITERAL: "mm:Literal"> }

TOKEN: { <MM_DEFAULT_VALUE_ENCODING: "mm:DefaultValueEncoding"> }
TOKEN: { <MM_DEFAULT_IRI_ENCODING: "mm:DefaultIRIEncoding"> }
TOKEN: { <MM_DEFAULT_REFERENCE_TYPE: "mm:DefaultReferenceType"> }
TOKEN: { <MM_DEFAULT_PROPERTY_TYPE: "mm:DefaultPropertyType"> }
TOKEN: { <MM_DEFAULT_PROPERTY_VALUE_TYPE: "mm:DefaultPropertyValueType"> }
TOKEN: { <MM_DEFAULT_DATA_PROPERTY_VALUE_TYPE: "mm:DefaultDataPropertyValueType"> }
TOKEN: { <MM_DEFAULT_ANNOTATION_PROPERTY_VALUE_TYPE: "mm:DefaultAnnotationPropertyValueType"> }
TOKEN: { <MM_DEFAULT_SHIFT: "mm:DefaultShift"> }

TOKEN: { <MM_DEFAULT_EMPTY_LITERAL: "mm:DefaultEmptyLiteral"> }
TOKEN: { <MM_DEFAULT_EMPTY_LOCATION: "mm:DefaultEmptyLocation"> }
TOKEN: { <MM_DEFAULT_EMPTY_RDF_ID: "mm:DefaultEmptyID"> }
TOKEN: { <MM_DEFAULT_EMPTY_RDFS_LABEL: "mm:DefaultEmptyLabel"> }
TOKEN: { <MM_DEFAULT_IF_OWL_ENTITY_EXISTS: "mm:DefaultIfOWLEntityExists"> }
TOKEN: { <MM_DEFAULT_IF_OWL_ENTITY_DOES_NOT_EXIST: "mm:DefaultIfOWLEntityDoesNotExist"> }

TOKEN: { <MM_UNTYPED: "mm:Untyped"> }
TOKEN: { <MM_PREFIX: "mm:Prefix"> }
TOKEN: { <MM_NAMESPACE: "mm:Namespace"> }
TOKEN: { <CELL_WILDCARD: "*"> }
TOKEN: { <RDFS_CLASS: "rdfs:class"> }
TOKEN: { <RDF_PROPERTY: "rdf:property"> }
TOKEN: { <OWL_CLASS: "Class"> }
TOKEN: { <OWL_NAMED_INDIVIDUAL: "Individual"> }
TOKEN: { <OWL_OBJECT_PROPERTY: "ObjectProperty"> }
TOKEN: { <OWL_DATA_PROPERTY: "DataProperty"> }
TOKEN: { <OWL_ANNOTATION_PROPERTY: "AnnotationProperty"> }
TOKEN: { <OWL_DATATYPE: "Datatype"> }
TOKEN: { <MM_IRI: "mm:IRI" > }

TOKEN: { <MM_TO_UPPER_CASE: "mm:toUpperCase"> }
TOKEN: { <MM_TO_LOWER_CASE: "mm:toLowerCase"> }
TOKEN: { <MM_TRIM: "mm:trim"> }
TOKEN: { <MM_PRINTF: "mm:printf"> }
TOKEN: { <MM_DECIMAL_FORMAT: "mm:decimalFormat"> }
TOKEN: { <MM_CAPTURING: "mm:capturing"> }
TOKEN: { <MM_REVERSE: "mm:reverse"> }
TOKEN: { <MM_REPLACE: "mm:replace"> }
TOKEN: { <MM_REPLACE_ALL: "mm:replaceAll"> }
TOKEN: { <MM_REPLACE_FIRST: "mm:replaceFirst"> }
TOKEN: { <MM_APPEND: "mm:append"> }
TOKEN: { <MM_PREPEND: "mm:prepend"> }

TOKEN: { <MM_NO_ENCODE: "mm:noEncode"> }
TOKEN: { <MM_CAMELCASE_ENCODE: "mm:camelCaseEncode"> }
TOKEN: { <MM_SNAKECASE_ENCODE: "mm:snakeCaseEncode"> }
TOKEN: { <MM_UUID_ENCODE: "mm:uuidEncode"> }
TOKEN: { <MM_HASH_ENCODE: "mm:hashEncode"> }

TOKEN: { <MM_DEFAULT_LOCATION_VALUE: "mm:DefaultLocationValue"> }
TOKEN: { <MM_DEFAULT_LITERAL: "mm:DefaultLiteral"> }
TOKEN: { <MM_DEFAULT_ID: "mm:DefaultID"> }
TOKEN: { <MM_DEFAULT_LABEL: "mm:DefaultLabel"> }

TOKEN: { <MM_DEFAULT_PREFIX: "mm:DefaultPrefix"> }
TOKEN: { <MM_DEFAULT_NAMESPACE: "mm:DefaultNamespace"> }
TOKEN: { <MM_DEFAULT_LANGUAGE: "mm:DefaultLanguage"> }

TOKEN: { <MM_NO_SHIFT: "mm:NoShift"> }
TOKEN: { <MM_SHIFT_UP: "mm:ShiftUp"> }
TOKEN: { <MM_SHIFT_DOWN: "mm:ShiftDown"> }
TOKEN: { <MM_SHIFT_LEFT: "mm:ShiftLeft"> }
TOKEN: { <MM_SHIFT_RIGHT: "mm:ShiftRight"> }

TOKEN: { <MM_ERROR_IF_EMPTY_LOCATION: "mm:ErrorIfEmptyLocation"> }
TOKEN: { <MM_WARNING_IF_EMPTY_LOCATION: "mm:WarningIfEmptyLocation"> }
TOKEN: { <MM_SKIP_IF_EMPTY_LOCATION: "mm:SkipIfEmptyLocation"> }
TOKEN: { <MM_PROCESS_IF_EMPTY_LOCATION: "mm:ProcessIfEmptyLocation"> }

TOKEN: { <MM_PROCESS_IF_EMPTY_LITERAL: "mm:ProcessIfEmptyLiteral"> }
TOKEN: { <MM_ERROR_IF_EMPTY_LITERAL: "mm:ErrorIfEmptyLiteral"> }
TOKEN: { <MM_WARNING_IF_EMPTY_LITERAL: "mm:WarningIfEmptyLiteral"> }
TOKEN: { <MM_SKIP_IF_EMPTY_LITERAL: "mm:SkipIfEmptyLiteral"> }

TOKEN: { <MM_PROCESS_IF_EMPTY_ID: "mm:ProcessIfEmptyID"> }
TOKEN: { <MM_ERROR_IF_EMPTY_ID: "mm:ErrorIfEmptyID"> }
TOKEN: { <MM_WARNING_IF_EMPTY_ID: "mm:WarningIfEmptyID"> }
TOKEN: { <MM_SKIP_IF_EMPTY_ID: "mm:SkipIfEmptyID"> }

TOKEN: { <MM_PROCESS_IF_EMPTY_LABEL: "mm:ProcessIfEmptyLabel"> }
TOKEN: { <MM_ERROR_IF_EMPTY_LABEL: "mm:ErrorIfEmptyLabel"> }
TOKEN: { <MM_WARNING_IF_EMPTY_LABEL: "mm:WarningIfEmptyLabel"> }
TOKEN: { <MM_SKIP_IF_EMPTY_LABEL: "mm:SkipIfEmptyLabel"> }

TOKEN: { <MM_RESOLVE_IF_OWL_ENTITY_EXISTS: "mm:ResolveIfOWLEntityExists"> }
TOKEN: { <MM_SKIP_IF_OWL_ENTITY_EXISTS: "mm:SkipIfOWLEntityExists"> }
TOKEN: { <MM_WARNING_IF_OWL_ENTITY_EXISTS: "mm:WarningIfOWLEntityExists"> }
TOKEN: { <MM_ERROR_IF_OWL_ENTITY_EXISTS: "mm:ErrorIfOWLEntityExists"> }
TOKEN: { <MM_CREATE_IF_OWL_ENTITY_DOES_NOT_EXIST: "mm:CreateIfOWLEntityDoesNotExist"> }
TOKEN: { <MM_SKIP_IF_OWL_ENTITY_DOES_NOT_EXIST: "mm:SkipIfOWLEntityDoesNotExist"> }
TOKEN: { <MM_WARNING_IF_OWL_ENTITY_DOES_NOT_EXIST: "mm:WarningIfOWLEntityDoesNotExist"> }
TOKEN: { <MM_ERROR_IF_OWL_ENTITY_DOES_NOT_EXIST: "mm:ErrorIfOWLEntityDoesNotExist"> }

TOKEN:
{
  <STRING_LITERAL: "\"" (~["\""])* ("\"\"" (~["\""])*)* ("\\\"")* "\"" >
| <QUOTED_NAME_LITERAL: "'" (~["'"])* ("''" (~["'"])*)* "'" >
| <IDENTIFIER: <LETTER> (<LETTER> | <DIGIT> | "_" | "-" | "#" | "." | ":" | "/" )* | "'" (~["'"])* "'"> 
| <IRI_REF: "<" (~[ ">","<", "\"", "{", "}", "^", "\\", "|", "`", "\u0000"-"\u0020"])* ">" >
| <INTEGER_LITERAL: (["-"])? (<DIGIT>)+ > 
| <LONG_LITERAL: (["-"])? (<DIGIT>)+> 
| <FLOAT_LITERAL:   (["-"])? (<DIGIT>)+ "." (<DIGIT>)+ (<EXPONENT>)?
                  | (["-"])? "." (<DIGIT>)+ (<EXPONENT>)?
                  | (["-"])? (<DIGIT>)+ <EXPONENT>
                  | (["-"])? (<DIGIT>)+ (<EXPONENT>)? > 
|
  <#LETTER: [ "a"-"z", "A"-"Z" ]>
|
  <#DIGIT: ["0"-"9"] >
| 
  <#EXPONENT: ["e", "E"] (["+", "-"])? (<DIGIT>)+ >
| 
  <LOCATION_SPECIFICATION: (<LETTER> | <DIGIT> | "_" | "-" | "#" | "." | ":" | "/" | "*" )+ | "'" (~["'"])* "'"> 
}

SimpleNode expression() #Expression : {}
{
  (  mm_directive()
   | mm_expression()
  ) <EOF>
  {
     return jjtThis;
  }
}

void mm_directive() #MMDirective : {}
{
  (  mm_default_value_encoding_directive()
   | mm_default_iri_encoding_directive()
   | mm_default_reference_type_directive()
   | mm_default_property_type_directive()
   | mm_default_property_value_type_directive()
   | mm_default_data_property_value_type_directive()
   | mm_default_shift_directive()
   | mm_default_empty_location_directive()
   | mm_default_empty_literal_directive()
   | mm_default_empty_rdf_id_directive()
   | mm_default_empty_rdfs_label_directive()
   | mm_default_if_exists_directive()
   | mm_default_if_not_exists_directive()
  ) 
}

void mm_expression() #MMExpression : {}
{
  (  owl_class_declaration() 
   | owl_individual_declaration()
  )
  // TODO datatype, object property, data property, and annotation property declarations
  // TODO negative property assertions, disjoint classes, disjoint properties, equivalent properties
  // See: http://www.w3.org/TR/owl2-manchester-syntax/
}

void mm_default_value_encoding_directive() #MMDefaultValueEncoding: 
{
  int encodingType;
}
{
  <MM_DEFAULT_VALUE_ENCODING> 
      (  <RDF_ID> { encodingType = RDF_ID; }  
       | <RDFS_LABEL> { encodingType = RDFS_LABEL; } 
       | <MM_LITERAL> { encodingType = MM_LITERAL; } 
       | <MM_LOCATION> { encodingType = MM_LOCATION; } 
      )
  {
    jjtThis.encodingType = encodingType;
    defaultValueEncoding = encodingType;
  }
}

void mm_default_iri_encoding_directive() #MMDefaultIRIEncoding: 
{
  int encodingType;
}
{
  <MM_DEFAULT_IRI_ENCODING> 
      (  <MM_NO_ENCODE> { encodingType = MM_NO_ENCODE; } 
       | <MM_CAMELCASE_ENCODE> { encodingType = MM_CAMELCASE_ENCODE; } 
       | <MM_SNAKECASE_ENCODE> { encodingType = MM_SNAKECASE_ENCODE; } 
       | <MM_UUID_ENCODE> { encodingType = MM_UUID_ENCODE; } 
       | <MM_HASH_ENCODE> { encodingType = MM_HASH_ENCODE; } 
      )
  {
    jjtThis.encodingType = encodingType;
    defaultIRIEncoding = encodingType;
  }
}

void mm_default_shift_directive() #MMDefaultShiftSetting: 
{
  int shiftSetting;
}
{
  <MM_DEFAULT_SHIFT> shiftSetting = shift_directive()

  {
    jjtThis.shiftSetting = shiftSetting;
    defaultShiftSetting = shiftSetting;
  }
}

void mm_default_empty_location_directive() #MMDefaultEmptyLocationSetting: 
{
  int emptyLocationSetting;
}
{
  <MM_DEFAULT_EMPTY_LOCATION> emptyLocationSetting = empty_location_directive()

  {
    jjtThis.emptyLocationSetting = emptyLocationSetting;
    defaultEmptyLocationSetting = emptyLocationSetting;
  }
}

void mm_default_empty_literal_directive() #MMDefaultEmptyLiteralSetting: 
{
  int emptyLiteralSetting;
}
{
  <MM_DEFAULT_EMPTY_LITERAL> emptyLiteralSetting = empty_literal_directive()

  {
    jjtThis.emptyLiteralSetting = emptyLiteralSetting;
    defaultEmptyLiteralSetting = emptyLiteralSetting;
  }
}

void mm_default_if_exists_directive() #MMDefaultIfExistsSetting: 
{
  int ifExistsSetting;
}
{
  <MM_DEFAULT_IF_OWL_ENTITY_EXISTS> ifExistsSetting = if_exists_directive()

  {
    jjtThis.ifExistsSetting = ifExistsSetting;
    defaultIfOWLEntityExistsSetting = ifExistsSetting;
  }
}

void mm_default_if_not_exists_directive() #MMDefaultIfNotExistsSetting: 
{
  int ifNotExistsSetting;
}
{
  <MM_DEFAULT_IF_OWL_ENTITY_DOES_NOT_EXIST> ifNotExistsSetting = if_not_exists_directive()

  {
    jjtThis.ifNotExistsSetting = ifNotExistsSetting;
    defaultIfOWLEntityDoesNotExistSetting = ifNotExistsSetting;
  }
}

void mm_default_empty_rdf_id_directive() #MMDefaultEmptyRDFIDSetting: 
{
  int emptyRDFIDSetting;
}
{
  <MM_DEFAULT_EMPTY_RDF_ID> emptyRDFIDSetting = empty_rdf_id_directive()

  {
    jjtThis.emptyRDFIDSetting = emptyRDFIDSetting;
    defaultEmptyRDFIDSetting = emptyRDFIDSetting;
  }
}

void mm_default_empty_rdfs_label_directive() #MMDefaultEmptyRDFSLabelSetting: 
{
  int emptyRDFSLabelSetting;
}
{
  <MM_DEFAULT_EMPTY_RDFS_LABEL> emptyRDFSLabelSetting = empty_rdfs_label_directive()

  {
    jjtThis.emptyRDFSLabelSetting = emptyRDFSLabelSetting;
    defaultEmptyRDFSLabelSetting = emptyRDFSLabelSetting;
  }
}


void mm_default_reference_type_directive() #MMDefaultReferenceType: 
{
  int referenceType;
}
{
  <MM_DEFAULT_REFERENCE_TYPE> referenceType = reference_type()

  {
    defaultReferenceType = referenceType;
  }
}

void mm_default_language() #MMDefaultLanguage: 
{
  String language;
}
{
  <MM_DEFAULT_LANGUAGE> language = identifier()

  {
    defaultLanguage = language;
  }
}

void mm_default_prefix() #MMDefaultPrefix: 
{
  String prefix;
}
{
  <MM_DEFAULT_PREFIX> prefix = string()

  {
    defaultPrefix = prefix;
  }
}

void mm_default_namespace() #MMDefaultNamespace: 
{
  String namespace;
}
{
  <MM_DEFAULT_NAMESPACE> namespace = string()

  {
    defaultNamespace = namespace;
  }
}

void mm_default_property_value_type_directive() #MMDefaultPropertyValueType: 
{
  int defaultType;
}
{
  <MM_DEFAULT_PROPERTY_VALUE_TYPE> defaultType = reference_type()
 {
   jjtThis.defaultType = defaultType;
   defaultPropertyValueType = defaultType;
 }
}

void mm_default_data_property_value_type_directive() #MMDefaultDatatypePropertyValueType: 
{
  int defaultType;
}
{
  <MM_DEFAULT_DATA_PROPERTY_VALUE_TYPE> defaultType = datatype()
 {
   jjtThis.defaultType = defaultType;
   defaultDataPropertyValueType = defaultType;
 }
}

void mm_default_annotation_property_value_type_directive() #MMDefaultAnnotationPropertyValueType: 
{
  int defaultReferenceType;
}
{
  <MM_DEFAULT_ANNOTATION_PROPERTY_VALUE_TYPE> defaultReferenceType = reference_type()
 {
   jjtThis.defaultType = defaultReferenceType;
   defaultAnnotationPropertyValueType = defaultReferenceType;
 }
}

void mm_default_property_type_directive() #MMDefaultPropertyType: 
{
  Token t;
}
{
  <MM_DEFAULT_PROPERTY_TYPE> (  t = <OWL_DATA_PROPERTY> 
                              | t = <OWL_OBJECT_PROPERTY>)
 {
   jjtThis.defaultType = t.kind;;
   defaultPropertyType = t.kind;
 }
}

void owl_class_declaration() #OWLClassDeclaration : {}
{
  <CLASS> owl_class(defaultClassValueEncoding)
    (  owl_equivalent_classes() 
     | owl_subclass_of()
    ) *
   ( <ANNOTATIONS> annotation_fact_list() )?
  // TODO Other clauses: DisjointWith, DisjoinUnionOf, HasKey
  // See: http://www.w3.org/TR/owl2-manchester-syntax/
}

void owl_equivalent_classes() #OWLEquivalentClasses : {}
{
   <EQUIVALENT_TO> owl_class_expression_with_condition() ( "," owl_class_expression_with_condition() )*
} 

void owl_subclass_of() #OWLSubclassOf : {}
{
   <SUBCLASS_OF> owl_class_expression_with_condition() ( "," owl_class_expression_with_condition() )*
} 

void owl_individual_declaration() #OWLIndividualDeclaration : {}
{ // TODO Order of subclauses should not matter
  <INDIVIDUAL> owl_named_individual(defaultIndividualValueEncoding) 
  ( <FACTS> fact_list() )?
  ( <TYPES> types(OWL_CLASS) )?
  ( <ANNOTATIONS> annotation_fact_list() )?
  ( owl_same_as() )?
  ( owl_different_from() )?
}

void owl_same_as() #OWLSameAs : {}
{
  <SAME_AS> owl_named_individual(defaultValueEncoding) ( "," owl_named_individual(defaultValueEncoding) )*
}

void owl_different_from() #OWLDifferentFrom : {}
{
  <DIFFERENT_FROM>  owl_named_individual(defaultValueEncoding) ( "," owl_named_individual(defaultValueEncoding) )*
}

void fact_list() : {}
{
  fact() ( "," fact() )*
}

void annotation_fact_list() : {}
{
  annotation_fact() ( "," annotation_fact() )*
}

void fact() #Fact: {}
{
  owl_property() owl_property_assertion_object(defaultPropertyValueType)
}

void annotation_fact() #AnnotationFact: {}
{
  owl_annotation_property() owl_annotation_value(defaultAnnotationPropertyValueType)
}

void owl_annotation_property() #OWLAnnotationProperty: {}
{
  owl_property_name() | reference(defaultPropertyType, defaultPropertyValueEncoding, defaultIRIEncoding)
}

void owl_class_name() : {}
{
  name()
}

void owl_property_name() : {}
{
  name()
}

void owl_individual_name() : {}
{
  name()
}

void name() #Name:
{
  String name;
  boolean isQuotedName = false;
}
{
  (  name = identifier() 
   | name = quoted_name() { isQuotedName = true; }
  )

  { 
    jjtThis.name = name; 
    jjtThis.isQuotedName = isQuotedName; 
  }
}

void owl_annotation_value(int defaultPropertyValueType) #OWLAnnotationValue : {}
{
  (  reference(defaultPropertyValueType, defaultValueEncoding, defaultIRIEncoding) 
   | name() 
   | owl_literal() 
   | iri_ref()
  )
}

void owl_literal() #OWLLiteral : {}
{
  (  integer_literal() 
   | float_literal() 
   | boolean_literal() 
   | string_literal() 
  )
} 

void integer_literal() #IntegerLiteral :
{
  Token t;
}
{
  t = <INTEGER_LITERAL>

  {
    jjtThis.value = Integer.valueOf(token.image);
  }
}

void float_literal() #FloatLiteral :
{
  Token t;
}
{
  t = <FLOAT_LITERAL>

  {
    jjtThis.value = Float.valueOf(token.image);
  }
}


String string() : 
{
  Token t;
}
{
  t = <STRING_LITERAL> 

  {
    String s = t.image;
    return s.substring(1, s.length() - 1);
  }
}

void string_literal() #StringLiteral :
{
  String value;
}
{
  value = string()

  {
    jjtThis.value = value;
  }
}

void boolean_literal() #BooleanLiteral :
{
  boolean value;
}
{
  ( <TRUE> { value = true; } | <FALSE> { value = false; } )

  {
    jjtThis.value = value;
  }
}

void iri_ref() #IRIRef : 
{
  Token t;
}
{
  t = <IRI_REF> 
  {
    jjtThis.value = t.image;
  }
}

void owl_class_expression() #OWLClassExpression :
{
  boolean isNegated = false;
}
{
  ( <NOT> { isNegated = true; } )? ( LOOKAHEAD(owl_restriction()) owl_restriction() 
                                     | owl_class(defaultClassValueEncoding) 
                                     | "(" owl_union_class() ")" ) // parenthesized union expression
                                   { jjtThis.isNegated = isNegated; }
}

void owl_class_expression_with_condition() #OWLClassExpression : {}
{
  owl_union_class()}

void owl_union_class() #OWLUnionClass : {}
{
  owl_intersection_class() ( <OR> owl_intersection_class() )*}

void owl_intersection_class() #OWLIntersectionClass : {}
{
  owl_class_expression() ( <AND> owl_class_expression() )*
}

void owl_object_one_of() #OWLObjectOneOf : {}
{
  "{" owl_named_individual(defaultIndividualValueEncoding) ( "," owl_named_individual(defaultIndividualValueEncoding) )* "}"
}

void owl_class(int defaultValueEncoding) #OWLClass : {}
{
  owl_class_name() | reference(OWL_CLASS, defaultValueEncoding, defaultIRIEncoding)
} 

void owl_named_individual(int defaultValueEncoding) #OWLNamedIndividual : {}
{
  owl_individual_name() | reference(OWL_NAMED_INDIVIDUAL, defaultValueEncoding, defaultIRIEncoding)
} 

void owl_property() #OWLProperty : {}
{
  owl_property_name() | reference(defaultPropertyType, defaultPropertyValueEncoding, defaultIRIEncoding)
} 

 
void types(int referenceType) #Types : 
{
  int defaultType = ParserUtil.getDefaultType(referenceType);
} 
{
  ( LOOKAHEAD(3) reference(referenceType, defaultValueEncoding, defaultIRIEncoding) | owl_class_expression() )
    ( "," ( LOOKAHEAD(3) reference(referenceType, defaultValueEncoding, defaultIRIEncoding) | owl_class_expression() ) )*
}

void value_specification_item() #ValueSpecificationItem : 
{
  String stringLiteral = null;
  String captureExpression = null;
}
{
  (  stringLiteral = string() 
   | reference(XSD_STRING, defaultValueEncoding, defaultIRIEncoding) 
   | value_extraction_function() 
//   | ( "[" captureExpression = string() "]" )
  )
  {
    jjtThis.stringLiteral = stringLiteral;
    jjtThis.captureExpression = captureExpression;
  }
}


String identifier() :
{
  Token t;
}
{
  t = <IDENTIFIER>

  { return t.image; }
}

String location_specification() :
{
  Token t;
}
{
  t = <LOCATION_SPECIFICATION>

  { return t.image; }
}

void source_specification() #SourceSpecification : 
{
  String source = null;
  String location = null;
  String literal = null;
  Token t;
}
{ 
  (
     ( ( source = quoted_name() "!" )? ( location = location_specification() | location = identifier() ) )
   | literal = string()
  )

  { 
     jjtThis.source = source;
     jjtThis.location = location;
     jjtThis.literal = literal;
  }
}

void owl_restriction() #OWLRestriction : {}
{
  owl_property() (  owl_max_cardinality_restriction()  
                  | owl_min_cardinality_restriction() 
                  | owl_exact_cardinality_restriction()
                  | owl_has_value_restriction() 
                  | owl_all_values_from_restriction() 
                  | owl_some_values_from_restriction() 
                 )
}

void owl_exact_cardinality_restriction() #OWLExactCardinalityRestriction :
{
  int cardinality;
}
{
  <EXACTLY> cardinality = cardinality_value()
  {
    jjtThis.cardinality = cardinality;
  }
}

void owl_max_cardinality_restriction() #OWLMaxCardinalityRestriction :
{
  int cardinality;
}
{
  <MAX> cardinality = cardinality_value()

  {
    jjtThis.cardinality = cardinality;
  }
}

void owl_min_cardinality_restriction() #OWLMinCardinalityRestriction :
{
  int cardinality;
}
{
   <MIN> cardinality = cardinality_value()

  {
    jjtThis.cardinality = cardinality;
  }
}

void owl_has_value_restriction() #OWLHasValueRestriction : {}
{
  <VALUE> (  reference(OWL_NAMED_INDIVIDUAL, defaultValueEncoding, defaultIRIEncoding) 
           | name() 
           | owl_literal() 
          )
}

void owl_all_values_from_restriction() #OWLAllValuesFromRestriction : {}
{
  LOOKAHEAD(2) owl_data_all_values_from() | LOOKAHEAD(2) owl_object_all_values_from() 
}

void owl_data_all_values_from() #OWLDataAllValuesFrom :
{
  int datatype;
}
{
  <ONLY> datatype = datatype()
  {
    jjtThis.datatype = datatype;
  }
}

void owl_object_all_values_from() #OWLObjectAllValuesFrom : {}
{
  <ONLY> (owl_class(defaultClassValueEncoding) 
          | "(" owl_class_expression() ")" // parenthesized class expression
          | owl_object_one_of()
         )
}

void owl_some_values_from_restriction() #OWLSomeValuesFromRestriction : {}
{
  LOOKAHEAD(2) owl_data_some_values_from() | LOOKAHEAD(2) owl_object_some_values_from() 
}

void owl_object_some_values_from() #OWLObjectSomeValuesFrom : {}
{
  <SOME> (owl_class(defaultClassValueEncoding) | "(" owl_class_expression() ")" )
}

void owl_data_some_values_from() #OWLDataSomeValuesFrom :
{
  int datatype;
}
{
  <SOME> datatype = datatype()
  {
    jjtThis.datatype = datatype;
  }
}


void owl_property_assertion_object(int defaultPropertyValueType) #OWLPropertyAssertionObject : {}
{
  (  reference(defaultPropertyValueType, defaultValueEncoding, defaultIRIEncoding) 
   | name() 
   | owl_literal() 
  )
}

int integer_value() :
{
  Token t;
}
{
  t=<INTEGER_LITERAL>
  {
    return Integer.parseInt(t.image);
  }
}

int cardinality_value() :
{
 int cardinality;
}
{
  cardinality  = integer_value()

  {
    return cardinality;
  }
}

// Note that this is not an exhuastive list of datatypes

int datatype() :
{
  Token t;
}
{
 (
     t = <XSD_STRING>
   | t = <XSD_DECIMAL>
   | t = <XSD_BYTE>
   | t = <XSD_SHORT> 
   | t = <XSD_INTEGER>
   | t = <XSD_LONG>
   | t = <XSD_FLOAT>
   | t = <XSD_DOUBLE>
   | t = <XSD_BOOLEAN>
   | t = <XSD_TIME>
   | t = <XSD_DATETIME>
   | t = <XSD_DATE>
   | t = <XSD_DURATION>
   | t = <RDF_PLAINLITERAL>
 )
  {
    return t.kind;
  }
}

void reference(int defaultReferenceType, int defaultValueEncoding, int defaultIRIEncoding) #Reference : {}
{
   "@" source_specification()
       ( LOOKAHEAD(2) 
         "(" ( reference_type() )?
             ( reference_directive(defaultValueEncoding, defaultIRIEncoding) )*
         ")"
       )?
  {
    jjtThis.defaultReferenceDirectives = new DefaultReferenceDirectives(
                        defaultReferenceType, defaultValueEncoding, defaultIRIEncoding,
                        defaultLocationValue, defaultLiteral, defaultID, defaultLabel,
                        defaultLanguage, defaultPrefix, defaultNamespace,
                        defaultShiftSetting, defaultEmptyLocationSetting,
                        defaultEmptyLiteralSetting, defaultEmptyRDFIDSetting,
                        defaultEmptyRDFSLabelSetting, defaultIfOWLEntityExistsSetting,
                        defaultIfOWLEntityDoesNotExistSetting);

  }
}

void reference_directive(int defaultValueEncoding, int defaultIRIEncoding) : {}
{
  (  namespace_directive()  
   | prefix_directive() 
   | language_directive()
   | value_extraction_function() 
   | value_encoding_directive(defaultValueEncoding) 
   | iri_encoding_directive(defaultIRIEncoding) 
   | default_location_value_directive() 
   | default_literal_directive() 
   | default_label_directive() 
   | default_id_directive() 
   | empty_literal_directive()
   | empty_location_directive()
   | empty_rdf_id_directive() 
   | empty_rdfs_label_directive() 
   | shift_directive() 
   | if_exists_directive()
   | if_not_exists_directive()
  ) 
}

int reference_type() #ReferenceType : 
{
  Token t;
  int referenceType = defaultReferenceType;
}
{
  (
     t = <RDFS_CLASS> { referenceType = t.kind; }
   | t = <RDF_PROPERTY> { referenceType = t.kind; }
   | t = <OWL_CLASS> { referenceType = t.kind; }
   | t = <OWL_NAMED_INDIVIDUAL> { referenceType = t.kind; }
   | t = <OWL_OBJECT_PROPERTY> { referenceType = t.kind; }
   | t = <OWL_DATA_PROPERTY> { referenceType = t.kind; }
   | t = <OWL_ANNOTATION_PROPERTY> { referenceType = t.kind; }
   | t = <MM_IRI> { referenceType = t.kind; }
   | referenceType = datatype()
  ) 

  {
    jjtThis.referenceType = referenceType;
    return referenceType;
  }
}

int value_encoding_directive(int defaultValueEncoding) #ValueEncoding : 
{
  int encodingType = defaultValueEncoding;
}
{
  (
      <MM_LOCATION> { encodingType = MM_LOCATION; }
    | <MM_LOCATION_WITH_DUPLICATES> { encodingType = MM_LOCATION_WITH_DUPLICATES; }
    |
     (
      (  <RDF_ID> { encodingType = RDF_ID; } 
       | <RDFS_LABEL> { encodingType = RDFS_LABEL; } 
       | <MM_LITERAL> { encodingType = MM_LITERAL; } 
      )
      ( value_specification() )?
    )
  )
  {
    jjtThis.encodingType = encodingType;
    return encodingType;
  }
}

int iri_encoding_directive(int defaultIRIEncoding) #IRIEncoding :
{
  int encodingType = defaultIRIEncoding;
}
{
  (
      <MM_NO_ENCODE> { encodingType = MM_NO_ENCODE; }
    | <MM_CAMELCASE_ENCODE> { encodingType = MM_CAMELCASE_ENCODE; }
    | <MM_SNAKECASE_ENCODE> { encodingType = MM_SNAKECASE_ENCODE; }
    | <MM_UUID_ENCODE> { encodingType = MM_UUID_ENCODE; }
    | <MM_HASH_ENCODE> { encodingType = MM_HASH_ENCODE; }
  )
  {
    jjtThis.encodingType = encodingType;
    return encodingType;
  }
}

int empty_location_directive() #EmptyLocationSetting :
{
  Token t;
  int emptyLocationSetting;
}
{
  (
     t = <MM_ERROR_IF_EMPTY_LOCATION> { emptyLocationSetting = t.kind; }
   | t = <MM_WARNING_IF_EMPTY_LOCATION> { emptyLocationSetting = t.kind; }
   | t = <MM_SKIP_IF_EMPTY_LOCATION> { emptyLocationSetting = t.kind; }
   | t = <MM_PROCESS_IF_EMPTY_LOCATION> { emptyLocationSetting = t.kind; }
  )

  {
    jjtThis.emptyLocationSetting = emptyLocationSetting;
    return emptyLocationSetting;
  }
}

int if_exists_directive() #IfExistsDirective :
{
  Token t;
  int ifExistsSetting;
}
{
  (
     t = <MM_RESOLVE_IF_OWL_ENTITY_EXISTS> { ifExistsSetting = t.kind; }
   | t = <MM_SKIP_IF_OWL_ENTITY_EXISTS> { ifExistsSetting = t.kind; }
   | t = <MM_WARNING_IF_OWL_ENTITY_EXISTS> { ifExistsSetting = t.kind; }
   | t = <MM_ERROR_IF_OWL_ENTITY_EXISTS> { ifExistsSetting = t.kind; }
  )

  {
    jjtThis.ifExistsSetting = ifExistsSetting;
    return ifExistsSetting;
  }
}

int if_not_exists_directive() #IfNotExistsDirective :
{
  Token t;
  int ifNotExistsSetting;
}
{
  (
     t = <MM_CREATE_IF_OWL_ENTITY_DOES_NOT_EXIST> { ifNotExistsSetting = t.kind; }
   | t = <MM_SKIP_IF_OWL_ENTITY_DOES_NOT_EXIST> { ifNotExistsSetting = t.kind; }
   | t = <MM_WARNING_IF_OWL_ENTITY_DOES_NOT_EXIST> { ifNotExistsSetting = t.kind; }
   | t = <MM_ERROR_IF_OWL_ENTITY_DOES_NOT_EXIST> { ifNotExistsSetting = t.kind; }
  )

  {
    jjtThis.ifNotExistsSetting = ifNotExistsSetting;
    return ifNotExistsSetting;
  }
}

int empty_literal_directive() #EmptyLiteralSetting :
{
  Token t;
  int emptyLiteralSetting;
}
{
  (
     t = <MM_ERROR_IF_EMPTY_LITERAL> { emptyLiteralSetting = t.kind; }
   | t = <MM_WARNING_IF_EMPTY_LITERAL> { emptyLiteralSetting = t.kind; }
   | t = <MM_SKIP_IF_EMPTY_LITERAL> { emptyLiteralSetting = t.kind; }
   | t = <MM_PROCESS_IF_EMPTY_LITERAL> { emptyLiteralSetting = t.kind; }
  )

  {
    jjtThis.emptyLiteralSetting = emptyLiteralSetting;
    return emptyLiteralSetting;
  }
}

int empty_rdfs_label_directive() #EmptyRDFSLabelSetting :
{
  Token t;
  int emptyRDFSLabelSetting;
}
{
  (
     t = <MM_ERROR_IF_EMPTY_LABEL> { emptyRDFSLabelSetting = t.kind; }
   | t = <MM_WARNING_IF_EMPTY_LABEL> { emptyRDFSLabelSetting = t.kind; }
   | t = <MM_SKIP_IF_EMPTY_LABEL> { emptyRDFSLabelSetting = t.kind; }
   | t = <MM_PROCESS_IF_EMPTY_LABEL> { emptyRDFSLabelSetting = t.kind; }
  )

  {
    jjtThis.emptyRDFSLabelSetting = emptyRDFSLabelSetting;
    return emptyRDFSLabelSetting;
  }
}

int empty_rdf_id_directive() #EmptyRDFIDSetting :
{
  Token t;
  int emptyRDFIDSetting;
}
{
  (
     t = <MM_ERROR_IF_EMPTY_ID> { emptyRDFIDSetting = t.kind; }
   | t = <MM_WARNING_IF_EMPTY_ID> { emptyRDFIDSetting = t.kind; }
   | t = <MM_SKIP_IF_EMPTY_ID> { emptyRDFIDSetting = t.kind; }
   | t = <MM_PROCESS_IF_EMPTY_ID> { emptyRDFIDSetting = t.kind; }
  )

  {
    jjtThis.emptyRDFIDSetting = emptyRDFIDSetting;
    return emptyRDFIDSetting;
  }
}

int shift_directive() #ShiftSetting :
{
  Token t;
  int shiftSetting;
}
{
  (
     t = <MM_SHIFT_UP> { shiftSetting = t.kind; }
   | t = <MM_SHIFT_DOWN> { shiftSetting = t.kind; }
   | t = <MM_SHIFT_LEFT> { shiftSetting = t.kind; }
   | t = <MM_SHIFT_RIGHT> { shiftSetting = t.kind; }
   | t = <MM_NO_SHIFT> { shiftSetting = t.kind; }
  )

  {
    jjtThis.shiftSetting = shiftSetting;
    return shiftSetting;
  }
}

void default_literal_directive() #DefaultLiteral :
{
  String defaultLiteral;
}
{
  <MM_DEFAULT_LITERAL> "=" defaultLiteral = string()

  {
    jjtThis.defaultLiteral = defaultLiteral;
  }
}

void default_location_value_directive() #DefaultLocationValue :
{
  String defaultLocationValue;
}
{
  <MM_DEFAULT_LOCATION_VALUE> "=" defaultLocationValue = string()

  {
    jjtThis.defaultLocationValue = defaultLocationValue;
  }
}

void default_id_directive() #DefaultID :
{
  String defaultRDFID;
}
{
  <MM_DEFAULT_ID> "=" defaultRDFID = string()

  {
    jjtThis.defaultID = defaultRDFID;
  }
}

void default_label_directive() #DefaultLabel :
{
  String defaultLabel;
}
{
  <MM_DEFAULT_LABEL> "=" defaultLabel = string()

  {
    jjtThis.defaultLabel = defaultLabel;
  }
}

void prefix_directive() #Prefix :
{
  String prefix;
}
{
  <MM_PREFIX> "=" prefix = string()
  {
    jjtThis.prefix = prefix;
  }
}

void language_directive() #Language :
{
  String language;
}
{
  <XML_LANG>  ( "=" (  <MM_NULL> { language = ""; }
                     | "*" { language = "*"; }
                     | language = string()
                    )
               |
                ( "!" "=" <MM_NULL> { language = "+"; } )
              )
  {
    jjtThis.language = language;
  }
}

void namespace_directive() #Namespace :
{
  String namespace;
}
{
  <MM_NAMESPACE> "=" namespace = string()
  {
    jjtThis.namespace = namespace;
  }
}

void value_specification() #ValueSpecification :
{}
{
    "=" (  value_specification_item() 
         | ( "(" value_specification_item() ( "," value_specification_item() )* ")" ) 
        )
}


void value_extraction_function() #ValueExtractionFunction:
{
  Token t;
  int functionID;
}
{
  ((
    ( 
       t = <MM_TO_UPPER_CASE> { functionID = t.kind; }
     | t = <MM_TO_LOWER_CASE> { functionID = t.kind; }
     | t = <MM_TRIM> { functionID = t.kind; }
     | t = <MM_PRINTF> { functionID = t.kind; }
     | t = <MM_DECIMAL_FORMAT> { functionID = t.kind; }
     | t = <MM_REVERSE> { functionID = t.kind; }
     | t = <MM_CAPTURING> { functionID = t.kind; }
     | t = <MM_REPLACE> { functionID = t.kind; }
     | t = <MM_REPLACE_ALL> { functionID = t.kind; }
     | t = <MM_REPLACE_FIRST> { functionID = t.kind; }
     | t = <MM_APPEND> { functionID = t.kind; }
     | t = <MM_PREPEND> { functionID = t.kind; }
    ) ( LOOKAHEAD(2) "(" value_extraction_function_argument() ( "," value_extraction_function_argument() )* ")" )?
   )
   |
   ( "[" ( LOOKAHEAD(2) value_extraction_function_argument() ( "," value_extraction_function_argument() )* )? "]" )  { functionID = MM_CAPTURING; } 
  )
  {
    jjtThis.functionID = functionID;
  }
}

void value_extraction_function_argument() #ValueExtractionFunctionArgument : {}
{
  reference(XSD_STRING, defaultValueEncoding, defaultIRIEncoding) | owl_literal()
}


List<String> strings() :
{
  List<String> strings = new ArrayList<String>();
  String s;
}
{
  s = string() { strings.add(s); } ( "," s = string() { strings.add(s); } )*

  { return strings; }
} 

String quoted_name() : 
{
  Token t;
}
{
  t = <QUOTED_NAME_LITERAL> 

  {
    String s = t.image;
    return s.substring(1, s.length() - 1);
  }
}